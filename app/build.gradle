import groovy.json.JsonSlurper

plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "ru.shirobokov.reanimation"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 7
        versionName "1.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/schemas")
            }
        }
    }

    def signKeys = new JsonSlurper().parseText(file('../signkeys.json').text)

    signingConfigs {
        release {
            storeFile file(signKeys.storeFile)
            storePassword signKeys.storePassword
            keyAlias signKeys.keyAlias
            keyPassword signKeys.keyPassword
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            manifestPlaceholders["crashlyticsEnabled"] = "false"
        }
        release {
            minifyEnabled true
            signingConfig signingConfigs.getByName("release")
            firebaseCrashlytics { mappingFileUploadEnabled true }
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            manifestPlaceholders["crashlyticsEnabled"] = "true"
        }
    }

    sourceSets.getByName("main") {
        java.srcDir("src/main/kotlin")
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation "androidx.core:core-ktx:1.3.2"
    implementation "androidx.appcompat:appcompat:1.2.0"
    implementation "androidx.constraintlayout:constraintlayout:2.0.4"

    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.2.0"

    implementation "com.google.android.material:material:1.2.1"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.8"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.8"

    implementation "org.koin:koin-androidx-viewmodel:2.2.2"
    implementation "org.koin:koin-androidx-fragment:2.2.2"
    implementation "org.koin:koin-androidx-scope:2.2.2"
    implementation "org.koin:koin-androidx-ext:2.2.2"

    implementation "androidx.room:room-runtime:2.2.6"
    kapt "androidx.room:room-compiler:2.2.6"
    implementation "androidx.room:room-ktx:2.2.6"

    implementation 'com.kirich1409.viewbindingpropertydelegate:viewbindingpropertydelegate:1.4.1'
    implementation 'com.airbnb.android:lottie:3.4.0'

    implementation platform('com.google.firebase:firebase-bom:26.3.0')
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-config'

    implementation 'com.android.billingclient:billing:3.0.2'

    testImplementation "junit:junit:4.13.1"
    androidTestImplementation "androidx.test.ext:junit:1.1.2"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.3.0"
}